# Parse CDS log like this to calculate statistics, generated by -Xlog:cds+resolve=debug
# in https://github.com/openjdk/leyden/tree/premain
# 
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [  8]: boot java/lang/System => boot java/lang/System
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [120]: boot java/lang/System => boot java/util/Map (not supertype)
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [246]: boot java/lang/System => boot java/util/Properties (not supertype)
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [267]: boot java/lang/System => boot java/lang/String (not supertype)
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [439]: boot java/lang/System => boot java/util/Iterator (not supertype)
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [573]: boot java/lang/System => boot jdk/internal/misc/Unsafe (not supertype)
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [602]: boot java/lang/System => boot java/lang/ClassLoader (not supertype)
# [0.922s][debug  ][cds,resolve] archived klass  CP entry [654]: boot java/lang/System => boot java/lang/Thread (not supertype)


while {![eof stdin]} {
    set line [gets stdin]
    set notsuper [regsub {..not supertype.$} $line "" line]
    if {[regexp {archived klass *CP entry.*: (.*) => (.*)} $line dummy from to]} {
        set floader [lindex $from 0]
        set from    [lindex $from 1]
        set tloader [lindex $to 0]
        set to      [lindex $to 1]

        if {[string index $to 0] == "\["} {
            # don't collect statics about array classes since they are tied to the "bottom" class.
            continue
        }

        set  resolved_any_klass($from) 1
        if {$notsuper} {
            incr resolved_self_or_super($from) 1
        } else {
            incr resolved_not_super($from) 1
            incr was_resolved_by_unrelated_types($to) 1
        }

        set key $floader,$tloader,$to
        if {![info exists seen($key)]} {
            set seen($key) 1
            incr uniq_ref($floader,$tloader) 1
            if {$notsuper} {
                incr uniq_ref_not_super($floader,$tloader) 1
            }
        }
    }
}

puts [format {%5d classes have resolved CONSTANT_Class entries} [array size resolved_any_klass]]
puts [format {%5d classes have resolved CONSTANT_Class entries that are not their super types} [array size resolved_not_super]]
puts [format {%5d classes were resolved by classes that are not their super types} [array size was_resolved_by_unrelated_types]]

foreach f {boot plat app} {
    foreach t {boot plat app} {
        catch {
            set n $uniq_ref($f,$t)
            puts -nonewline [format "%-4s loader resolved %5d klasses in %-4s loader" $f $n $t]
            if {[info exists uniq_ref_not_super($f,$t)]} {
                puts [format ", of which %5d klasses are not super types" $uniq_ref_not_super($f,$t)]
            } else {
                puts ""
            }
        }
    }
}
